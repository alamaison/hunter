# Copyright (c) 2013, Ruslan Baratov
# All rights reserved.

cmake_minimum_required(VERSION 3.0)

include(ExternalProject) # ExternalProject_Add

# Scheme for download and install OpenSSL library

list(APPEND CMAKE_MODULE_PATH "@HUNTER_SELF@/cmake/modules")

include(hunter_assert_lists_equal)
include(hunter_status_debug)
include(hunter_test_string_not_empty)

hunter_status_debug("Scheme: url_sha1_openssl")

# Check preconditions
hunter_test_string_not_empty("@HUNTER_BASE@")
hunter_test_string_not_empty("@HUNTER_SELF@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_BASENAME@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_URL@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SHA1@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_DOWNLOAD_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_INSTALL_DIR@")

hunter_assert_lists_equal(@HUNTER_DOWNLOAD_SCHEME_VARIANTS@)

if(@HUNTER_STATUS_DEBUG@)
  set(CMAKE_VERBOSE_MAKEFILE ON)
endif()

set_directory_properties(
    PROPERTIES
    EP_BASE
    "@HUNTER_BASE@"
)

if(APPLE)
  set(configure_command "./Configure")
  set(configure_opts "darwin64-x86_64-cc")
  set(build_command . "@HUNTER_SELF@/scripts/clear-all.sh" && make)
else()
  set(configure_command "./config")
  set(build_command make)
endif()

set(configure_opts ${configure_opts} threads no-shared)

ExternalProject_Add(
    @HUNTER_PACKAGE_BASENAME@
    URL
    @HUNTER_PACKAGE_URL@
    URL_HASH
    SHA1=@HUNTER_PACKAGE_SHA1@
    DOWNLOAD_DIR
    "@HUNTER_PACKAGE_DOWNLOAD_DIR@"
    SOURCE_DIR
    "@HUNTER_BASE@/Source/@HUNTER_PACKAGE_BASENAME@"
        # Out of source build is not working.
        # Sources will be unpacked for each tag for different directories
        # and build in source.
    INSTALL_DIR
    "@HUNTER_PACKAGE_INSTALL_DIR@"
        # not used, just avoid creating Install/<name> empty directory
    CONFIGURE_COMMAND
    ${configure_command}
    ${configure_opts}
    "--prefix=@HUNTER_PACKAGE_INSTALL_DIR@"
    BUILD_COMMAND
    ${build_command}
    BUILD_IN_SOURCE
    1
    INSTALL_COMMAND
    make install_sw
    # Install without documentation
    # * https://github.com/openssl/openssl/issues/57
)
